function void latextab (const matrix mprint,
                        const string cnames "Space sep. string of columns",
                        const string rnames "Space sep. string of rows",
                        const string capt,
                        const string lab,
                        const string note,
                        const string path,
                        const int dec[0:6:3],
                        const string fontsize,
                        const bool todoc[FALSE])
    /* Main function */

    string sdec  = sprintf("%d", dec)
    scalar nc = cols(mprint)
    scalar nr = rows(mprint)

    catch outfile "@path" --quiet
        print_document_head(todoc)

        printf "\begin{table}[htbp]\n"
        printf "\centering\n"
        printf "%s \n", fontsize

        if note!=""
            printf "\begin{threeparttable} \n"
        endif

        printf "\begin{tabular}{l"
        loop i=1..nc
            printf "c"
        endloop
        printf "}\n"
        printf "\\toprule\n"

        printf "%s", strsplit(cnames,1)
        loop i=2..(1+nc)
            printf " & %s", strsplit(cnames,$i)
        endloop
        printf "\\\ \hline\n"

        loop i=1..nr
            printf "%s", strsplit(rnames,$i)
            loop j=1..nc
                if isnan(mprint[i,j])
                    printf " & "
                else
                    printf " & %.@sdecf", mprint[i,j]
                endif
            endloop
            printf "\\\ \n"
        endloop

        printf "\bottomrule\n"
        printf "\end{tabular}\n"

        if note!=""
            printf "\begin{tablenotes} \n"
            printf "\footnotesize \n"
            printf "\item \\textsc{Note:} @note \n"
            printf "\end{tablenotes} \n"
            printf "\end{threeparttable} \n"
        endif

        printf "\caption{@capt}\n"
        printf "\label{@lab}\n"
        printf "\end{table}\n"

        print_document_end(todoc)
    end outfile

    scalar err = $error

    if err
        printf "\nERROR: Failed to write tex-file.\n"
    else
        printf "\nINFO: Successfully wrote tex-file to '%s'.\n", path
    endif
end function


function void print_document_head (const bool todoc)
    if todoc == TRUE
        print "\documentclass{article}"
        print "\usepackage{booktabs}"
        print "\usepackage{threeparttable}"

        print "\begin{document}"
    endif
end function


function void print_document_end (const bool todoc)
    if todoc == TRUE
        print "\end{document}"
    endif
end function

